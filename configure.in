dnl **************************************************************
dnl Process this file with autoconf to produce a configure script.
dnl **************************************************************

AC_INIT(adonthell, 0.4dev1, adonthell@linuxgames.com)

dnl Detect the canonical host and target build environment
AC_CANONICAL_HOST
AC_CANONICAL_TARGET

AM_INIT_AUTOMAKE(adonthell, 0.4dev1)
AM_CONFIG_HEADER(config.h)

CFLAGS="-g -Wall -fno-exceptions -fno-rtti"
DEFS=""

dnl ********************
dnl Checks for programs.
dnl ********************

AC_PROG_CC
AC_PROG_CXX
AC_LANG_C
AC_PROG_CXXCPP
AM_PROG_LIBTOOL
AC_PROG_MAKE_SET

dnl ****
dnl i18n
dnl ****

AM_GNU_GETTEXT
ALL_LINGUAS=""

dnl ********************
dnl Additional arguments
dnl ********************

AC_ARG_ENABLE(tools,
[  --enable-tools         Build the Adonthell dev tools (disabled by default)],
	tools=$enableval, tools=no)
AC_ARG_ENABLE(memleaks,
[  --enable-memleaks      Enable check for memory leaks (disabled by default)],
	memleaks=$enableval, memleaks=no)
AC_ARG_ENABLE(doc,
[  --enable-doc           Build the Adonthell developer documentation (disabled by default)],
	builddoc=$enableval, builddoc=no)
AC_ARG_ENABLE(py-debug,
[  --enable-py-debug      Enable debugging of Python scripts (disabled by default)],
	pydebug=$enableval, pydebug=no)
AC_ARG_WITH(ogg,
[  --with-ogg=DIR         Set where the Ogg library is located]) 
AC_ARG_WITH(vorbis,
[  --with-vorbis=DIR      Set where the Vorbis library is located])
AC_ARG_WITH(data-dir,
[  --with-data-dir=DIR    Directory where datas will be installed],
            gamedatadir="$withval", gamedatadir=none)

dnl **********************
dnl Set the data directory
dnl **********************

if test x$gamedatadir = xnone; then
  case "$target" in
    *-*-cygwin* | *-*-mingw32*)
      gamedatadir="C:\share\adonthell"
    ;;
    *-*-beos*)
      gamedatadir="/boot/home/share/adonthell"
    ;;
    *)
      gamedatadir="${datadir}/adonthell"
    ;;
  esac
fi
DEFS="$DEFS -DDATA_DIR=\"\\\"$gamedatadir\\\"\""

dnl **********************
dnl Check for memory leaks
dnl **********************

if test x$memleaks = xyes; then
    DEFS="$DEFS -DMEMORY_LEAKS"
fi

dnl ********************
dnl Check for libraries.
dnl ********************

AC_CHECK_LIB(pthread, main,
    PTHREADS=yes,
    PTHREADS=no)

AC_CHECK_LIB(z, main,,echo "Adonthell requires Zlib. Exitting...";exit 1)

dnl *************
dnl Check for SDL
dnl *************

SDL_VERSION=1.2.0
AM_PATH_SDL($SDL_VERSION,
    :,
	AC_MSG_ERROR([*** SDL version $SDL_VERSION not found!])
)

dnl *******************
dnl Check for FreeType2
dnl *******************

AC_CHECK_FT2(6.0.0,
    :,
    AC_MSG_ERROR([*** Freetype version >= 6.0.0 not found!])
)

dnl *******************
dnl Check for OggVorbis
dnl *******************

OGG_VORBIS=no
AM_PATH_VORBIS(
        OGG_VORBIS="yes"
        ogg_music="enabled"
        OGG_LIBS="$VORBIS_LIBS $VORBISFILE_LIBS"
        OGG_DEFS="-DOGG_MUSIC"
        OGG_CFLAGS="$VORBIS_CFLAGS",
        AC_MSG_RESULT(Disabling OggVorbis support)
        ogg_music="disabled"
)

dnl ********************
dnl Check for Python. 
dnl ********************

AM_PATH_PYTHON()

dnl Find Python executable
AC_PATH_PROGS(PYPACKAGE, python)

dnl Extract the version using Python, check against 1.5+

changequote(<<, >>)
PYTHON_VERSION=`$PYPACKAGE -c 'import sys ; print (int(sys.version[0]) * 10 + int(sys.version[2]))'`
PY_VERSION=`$PYPACKAGE -c 'import sys ; print sys.version[0:3]'`
changequote([, ])

if test $PYTHON_VERSION -lt 15; then
	echo "Sorry, you need to have Python 1.5+ installed - update your version!"
	AC_MSG_ERROR([Python 1.5 or better required])
fi

dnl Find the Python.h header file

AC_MSG_CHECKING(for Python header files)
changequote(<<, >>)
PYINCLUDE=`$PYPACKAGE -c 'import sys ; print "%s/include/python%s" % (sys.prefix, sys.version[:3])'`
changequote([, ])

if test -r "$PYINCLUDE/Python.h"; then
   PY_CFLAGS="-I$PYINCLUDE"
else
   AC_MSG_ERROR([Could not find Python.h in $PYINCLUDE])
fi
AC_MSG_RESULT(found)


dnl Find the Python library

AC_MSG_CHECKING(for Python library)
changequote(<<, >>)
PYLIB=`$PYPACKAGE -c 'import sys; print "%s/lib/python%s/config" % (sys.prefix, sys.version[:3])'`
PYLIBVER=`$PYPACKAGE -c 'import sys; print sys.version[:3]'`
changequote([, ])


dnl Try for specific version first, then the generic version, then panic

if test -r "$PYLIB/libpython$PYLIBVER.so"; then
    PY_LIBS="-L$PYLIB -lpython$PYLIBVER $PY_LIBS"
elif test -r "$PYLIB/libpython$PYLIBVER.a"; then
    PY_LIBS="-L$PYLIB -lpython$PYLIBVER $PY_LIBS"
elif test -r "$PYLIB/libpython.so"; then
    PY_LIBS="-L$PYLIB -lpython $PY_LIBS"
elif test -r "$PYLIB/libpython.a"; then
    PY_LIBS="-L$PYLIB -lpython $PY_LIBS"
else
    AC_MSG_ERROR([Python library not found in $PYLIB])
fi
AC_MSG_RESULT(found)


dnl Get the libraries that python depends on

AC_MSG_CHECKING(for Python's dependencies)

changequote(<<, >>)
py_deps=`ldd $PYPACKAGE | sed 's/\( *lib\([^\/]*\)\.so.*=.*$\)/-l\2/p; d'`
for py_lib in $py_deps ; do
    if test "$py_lib" != "-lm" && test "$py_lib" != "-lc" ; then
        PY_DEPS="$PY_DEPS $py_lib"
    fi
done
changequote([, ])

AC_MSG_RESULT($PY_DEPS)

PY_LIBS="-Wl,-E $PY_LIBS $PY_DEPS"


dnl See whether we should enable python debugging

AC_MSG_CHECKING(whether to enable Python debugging)
if test x$pydebug = xno; then
	AC_MSG_RESULT(no)
else
    PY_CFLAGS="$PY_CFLAGS -DPY_DEBUG"
	AC_MSG_RESULT(yes)
fi


dnl **************
dnl Check for SWIG
dnl **************

haveswig=no

AC_PATH_PROG(P_SWIG,swig,no)
if test "$P_SWIG" != "no" ; then
    $P_SWIG -version &> swig.ver

    changequote(<<, >>)
    swig_major_ver=`more swig.ver | sed 's/.* \([0-9]*\)\.[0-9]*\.[0-9]*.*/\1/p; d'`
    swig_minor_ver=`more swig.ver | sed 's/.* [0-9]*\.\([0-9]*\)\.[0-9]*.*/\1/p; d'`
    swig_micro_ver=`more swig.ver | sed 's/.* [0-9]*\.[0-9]*\.\([0-9]*\).*/\1/p; d'`
    changequote([, ])

    rm -f swig.ver

    SWIG_VERSION="$swig_major_ver.$swig_minor_ver.$swig_micro_ver"

    if test $swig_major_ver -gt 1 -o \
            $swig_major_ver -eq 1 -a $swig_minor_ver -gt 3 -o \
            $swig_major_ver -eq 1 -a $swig_minor_ver -eq 3 -a $swig_micro_ver -gt 9 ;
    then
        haveswig=yes
    fi
fi

dnl SWIG is needed to compile the code from CVS
dnl or after making changes to the distributed code

if test x$haveswig = xno ; then
    if test -d "CVS"; then
        echo "*** To compile Adonthell from CVS, you will need SWIG 1.3.10 or higher."
        echo "*** You can obtain the most recent version from http://www.swig.org."
        exit 1;
    else
        echo "*** NOTE: If you plan to make changes to the code you will need"
        echo "*** SWIG 1.3.10 or higher. SWIG can be found at http://www.swig.org."
    fi
fi

dnl ********************************
dnl Check whether to build our tools
dnl ********************************

AC_MSG_CHECKING(whether to build the various editors)
if test x$tools = xno; then
	AC_MSG_RESULT(no)
	AM_CONDITIONAL(BUILD_TOOLS, false)
else
	AM_CONDITIONAL(BUILD_TOOLS, true)
	AC_MSG_RESULT(yes)
fi


dnl ***************************
dnl For some tools we need GTK+
dnl ***************************

if test x$tools = xyes; then
	AM_PATH_GTK(1.2.5, havegtk=yes, havegtk=no)

	if test "$havegtk" = yes; then
		AM_CONDITIONAL(HAVE_GTK, true)
	else
		AC_MSG_RESULT(disabling tools that need GTK+)
		AM_CONDITIONAL(HAVE_GTK, false)
	fi
fi

dnl ****************************************
dnl Check whether to build the documentation
dnl ****************************************

AC_MSG_CHECKING(whether to build the developer documentation)
if test x$builddoc = xno; then
	AC_MSG_RESULT(no)
	AM_CONDITIONAL(BUILD_DOC, false)
else
	AM_CONDITIONAL(BUILD_DOC, true)
	AC_MSG_RESULT(yes)
fi

dnl *****************************************************
dnl Doxygen and dot are needed to build the documentation
dnl *****************************************************

if test x$builddoc = xyes; then
        AC_PATH_PROG(P_DOXYGEN,doxygen)
        if test x$P_DOXYGEN == x ; then
                echo "Adonthell documentation requires doxygen. Exiting..."
                exit 1
        fi
        AC_PATH_PROG(P_DOT,dot)
        if test x$P_DOT == x ; then
                echo "Adonthell documentation requires dot. Exiting..."
                exit 1
        fi
fi        

dnl  **************************
dnl  Check for required headers
dnl  **************************

AC_CHECK_HEADERS([SDL/SDL.h zlib.h])

if test x$PTHREADS = xyes; then
   AC_CHECK_HEADERS([pthread.h])
fi

if test x$OGG_VORBIS = xyes; then
   AC_CHECK_HEADERS([vorbis/vorbisfile.h]) 
   AM_CONDITIONAL(HAVE_OGG_VORBIS, true)
else
   AM_CONDITIONAL(HAVE_OGG_VORBIS, false)
fi 


dnl ********************************
dnl Generate our compiler arguements
dnl ********************************

CXXFLAGS="$CFLAGS $DEFS $INCLUDES"

AC_SUBST(CXX) 
AC_SUBST(CXXFLAGS) 
AC_SUBST(LIBS) 
AC_SUBST(SDL_CFLAGS)
AC_SUBST(SDL_LIBS)
AC_SUBST(SDL_DEFS)
AC_SUBST(OGG_CFLAGS)
AC_SUBST(OGG_LIBS)
AC_SUBST(OGG_DEFS)
AC_SUBST(GTK_CFLAGS)
AC_SUBST(GTK_LIBS)
AC_SUBST(PY_CFLAGS)
AC_SUBST(PY_LIBS)
AC_SUBST(P_SWIG)
AC_SUBST(gamedatadir)
AC_SUBST(FT2_LIBS)
AC_SUBST(FT2_CFLAGS)

AC_OUTPUT([
Makefile
doc/Makefile
doc/devel/Makefile
doc/dlgedit/Makefile
doc/items/Makefile
intl/Makefile 
po/Makefile.in 
src/Makefile
src/tools/Makefile
src/tools/animedit/Makefile
src/tools/charedit/Makefile
src/tools/combat/Makefile
src/tools/dlgedit/Makefile
src/tools/dlgedit/examples/Makefile
src/tools/questedit/Makefile
src/tools/oggloop/Makefile
src/tools/pydonthell/Makefile
src/tools/animedit/Makefile
])


dnl ****************
dnl Configure Status
dnl ****************

echo
echo "Adonthell v$VERSION"
echo
echo "SDL ....................... : `$SDL_CONFIG --version`"
echo "Python .................... : $PY_VERSION"
echo "Python debugging .......... : $pydebug"
echo "Freetype .................. : `freetype-config --version`"
if test x$haveswig = xyes; then
echo "SWIG ...................... : $SWIG_VERSION"
fi
if test x$havegtk = xyes; then
echo "GTK+ ...................... : `gtk-config --version`"
fi
echo "Ogg music ................. : $ogg_music"
echo "Building editors .......... : $tools"
echo "Building API doc .......... : $builddoc"
if test x$memleaks = xyes; then
echo "Test for Memory Leaks ..... : enabled"
fi
echo "Data directory ............ : $gamedatadir"

echo
echo "Now type 'make' to build Adonthell"
echo "Afterwards, 'make install' will install Adonthell in $prefix/bin"
echo


